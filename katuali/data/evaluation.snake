from functools import partial

import katuali

rule get_depth:
    input:
        venv = ancient(config["RUNTIME"]["IN_POMOXIS"]),
        bam = ancient("{dir}/calls2ref.bam"),
    output:
        depth = directory("{dir}/depth")
    log:
        "{dir}/depth/get_depth.log"
    params:
        contig_opt = partial(get_contig_opt, config=config),
    resources:
        gpu = 0
    shell:
        """
        check_files_exist {config[RUNTIME][CHECK_FILES_EXIST_OPTS]} {input} &> {log}
        set +u; {config[RUNTIME][SOURCE]} {input.venv}; set -u;
        mkdir -p {output.depth} && coverage_from_bam {input.bam} {params.contig_opt} -s 1000 -p {output.depth}/ &>> {log}
        """


rule get_basecall_stats:
    input:
        venv = ancient(config["RUNTIME"]["IN_POMOXIS"]),
        bam = ancient("{dir}/calls2ref.bam"),
    output:
        stats = "{dir}/calls2ref_stats.txt"
    log:
        "{dir}/basecall_stats.log"
    resources:
        gpu = 0
    shell:
        """
        check_files_exist {config[RUNTIME][CHECK_FILES_EXIST_OPTS]} {input} &> {log}
        set +u; {config[RUNTIME][SOURCE]} {input.venv}; set -u;
        stats_from_bam {input.bam} -o {output.stats} &>> {log}
        """


rule assess_consensus:
    input:
        venv = ancient(config["RUNTIME"]["IN_POMOXIS"]),
        consensus = ancient("{runid}/{dir}/consensus.fasta"),
        consensus_mmi = ancient("{runid}/{dir}/consensus.fasta.mmi"),
        consensus_fai = ancient("{runid}/{dir}/consensus.fasta.fai"),
        truth = ancient(partial(get_reference, config=config)),
    output:
        summ = "{runid,[^/]+}/{dir}/assess{suffix,[^/]*}/consensus_to_truth_summ.txt",
        bam = "{runid,[^/]+}/{dir}/assess{suffix,[^/]*}/consensus_to_truth_summ.bam",
        stats = "{runid,[^/]+}/{dir}/assess{suffix,[^/]*}/consensus_to_truth_summ.txt",
    log:
        "{runid}/{dir}/assess{suffix,[^/]*}/assess_consensus{suffix}.log"
    params:
        prefix = "{runid,[^/]+}/{dir}/assess{suffix,[^/]*}/consensus_to_truth",
        opts = partial(get_opts, config=config, config_key="ASSESS_ASSM_OPTS")
    threads: config["RUNTIME"]["THREADS_PER_JOB"]
    resources:
        gpu = 0
    shell:
        """
        check_files_exist {config[RUNTIME][CHECK_FILES_EXIST_OPTS]} {input} &> {log}
        set +u; {config[RUNTIME][SOURCE]} {input.venv}; set -u;
        rm -rf {params.prefix}*
        assess_assembly -i {input.consensus} -r {input.truth} -p {params.prefix} -t {threads} {params[opts]} &>> {log}
        """
