rule medaka_train_features:
    input:
        in_medaka = ancient(config["RUNTIME"]["IN_MEDAKA"]),
        in_pomoxis = ancient(config["RUNTIME"]["IN_POMOXIS"]),
        draft = ancient("{runid}/{caller}/align/{region}/{dir}/consensus.fasta.gz"),
        draft_mmi = ancient("{runid}/{caller}/align/{region}/{dir}/consensus.fasta.gz.mmi"),
        draft_fai = ancient("{runid}/{caller}/align/{region}/{dir}/consensus.fasta.gz.fai"),
        basecalls = ancient("{runid}/{caller}/align/{region}/{dir}/basecalls.fastq.gz"),
        truth = ancient(partial(get_reference, config=config)),
    output:
        features = "{runid,[^/]+}/{caller,[^/]+}/align/{region,[^/]+}/{dir}/medaka_features{suffix,[^/]*}/medaka_train.hdf",
        rc_features = "{runid,[^/]+}/{caller,[^/]+}/align/{region,[^/]+}/{dir}/medaka_features{suffix,[^/]*}/medaka_train_rc.hdf",
    log:
        "{runid}/{caller}/align/{region}/{dir}/medaka_features{suffix}.log"
    threads: config["RUNTIME"]["THREADS_PER_JOB"]
    resources:
        gpu = 0
    params:
        output_dir = lambda w, output: os.path.dirname(output.features),
        bam = lambda w, output: os.path.join(os.path.dirname(output.features), "calls2ref"),
        truth_bam = lambda w, output: os.path.join(os.path.dirname(output.features), "truth2ref"),
        rc_bam = lambda w, output: os.path.join(os.path.dirname(output.features), "callsrc2ref"),
        rc_truth_bam = lambda w, output: os.path.join(os.path.dirname(output.features), "truth2refrc"),
        rc_draft = lambda w, output: os.path.join(os.path.dirname(output.features), "draftrc.fasta"),
        truth_region = lambda w, output: os.path.join(os.path.dirname(output.features), "truth.fasta"),
        opts = partial(get_opts, config=config, config_key="MEDAKA_TRAIN_FEAT_OPTS"),
        truth_chunk = 10000  # size of truth chunks aligned to draft to provide labels.
    shell:
        """
        check_files_exist {config[RUNTIME][CHECK_FILES_EXIST_OPTS]} {input} &> {log}

        if [[ "{params[opts]}" == *"HardRLEFeatureEncoder"* ]]
          then
          echo 'RLE encoding input files' >>{log}
          set +u; {config[RUNTIME][SOURCE]} {input.in_medaka}; set -u;

          # Compress basecalls into a .fastrle file
          basecalls={params.output_dir}/$(basename {input.basecalls}).fastrle
          medaka fastrle {input.basecalls} > ${{basecalls}} 2>> {log}

          # Compress truth into a .fastrle file
          truth_region={params.truth_region}.fastrle
          samtools faidx {input.truth} {wildcards.region} > {params.truth_region} 2>> {log}
          medaka fastrle {params.truth_region} > ${{truth_region}} 2>> {log}

          # Compress draft into a .fasta file
          draft_rle={params.output_dir}/$(basename {input.draft}).fastrle
          draft=${{draft_rle}}.fasta
          medaka fastrle {input.draft} > ${{draft_rle}} 2>> {log}
          set +u; {config[RUNTIME][SOURCE]} {input.in_pomoxis}; set -u;
          cat ${{draft_rle}} | fast_convert qa > ${{draft}} 2>> {log}

          # Define alignment scores
          ALIGNMENT_SCORES="-M 5 -S 4 -O 2 -E 3"
        else
          basecalls={input.basecalls}
          truth_region={params.truth_region}
          samtools faidx {input.truth} {wildcards.region} > ${{truth_region}} 2>> {log}
          draft={input.draft}
          ALIGNMENT_SCORES="-M 2 -S 4 -O 4,24 -E 2,1"
        fi

        set +u; {config[RUNTIME][SOURCE]} {input.in_pomoxis}; set -u;

        # keep a link of basecalls with the consensus
        ln -sf $PWD/{input.basecalls} $PWD/{params.output_dir}/basecalls.fastq.gz
        sleep 1

        echo "aligning basecalls to draft" >>{log}
        mini_align -i ${{basecalls}} -r ${{draft}} -p {params.bam} -t {threads} -P ${{ALIGNMENT_SCORES}} &>> {log}
        sleep 5

        echo "aligning truth to draft" >>{log}
        mini_align -i ${{truth_region}} -r ${{draft}} -p {params.truth_bam} -t {threads} -P -m -c {params.truth_chunk} ${{ALIGNMENT_SCORES}} &>> {log}
        sleep 5

        echo "reverse complement the draft and align reads" >> {log}
        seqkit seq --complement --reverse ${{draft}} -o {params.rc_draft} &>> {log}
        sleep 5

        echo "aligning basecalls to rc draft" >>{log}
        mini_align -i ${{basecalls}} -r {params.rc_draft} -p {params.rc_bam} -t {threads} -P -m ${{ALIGNMENT_SCORES}} &>> {log}
        sleep 5

        echo "aligning truth to rc draft" >> {log}
        mini_align -i ${{truth_region}} -r {params.rc_draft} -p {params.rc_truth_bam} -t {threads} -P -m -c {params.truth_chunk} ${{ALIGNMENT_SCORES}} &>> {log}
        sleep 5

        echo "creating features" >> {log}
        set +u; {config[RUNTIME][SOURCE]} {input.in_medaka} set -u;
        medaka features {params.bam}.bam {output.features} --truth {params.truth_bam}.bam {params[opts]} --threads {threads} &>> {log}
        echo "creating rc features"
        medaka features {params.rc_bam}.bam {output.rc_features} --truth {params.rc_truth_bam}.bam {params[opts]} --threads {threads} &>> {log}

        """


def get_medaka_train_features(wildcards, config=config):
    # The `MEDAKA_TRAIN_REPLICATES` section of the config specifies
    # a set of training replicates and the PIPELINE required to create the
    # features for these replicates. The PIPELINE itself is simply a list of
    # target templates. The function `expand_target_template` used below
    # fills in the template using values from the DATA section of the config
    # in all possible ways.
    features_pipeline = config["MEDAKA_TRAIN_REPLICATES"][wildcards["suffix"]]
    feature_templates = config["PIPELINES"][features_pipeline]
    targets = []
    for template in feature_templates:
        targets.extend(katuali.expand_target_template(template, config))

    if config["USE_ONLY_EXISTING_MEDAKA_FEAT"]:
        logger.run_info("WARNING: USE_ONLY_EXISTING_MEDAKA_FEAT set to true, only using existing medaka training features.")
        n_all_feat = len(targets)
        targets = [f for f in targets if os.path.isfile(f)]
        if len(targets) == 0:
            raise ValueError("USE_ONLY_EXISTING_MEDAKA_FEAT set to true in config, but no features were found")
        logger.run_info("Found {}/{} of the medaka training feature files.".format(len(targets), n_all_feat))

    return targets


rule medaka_train:
    input:
        venv = ancient(config["RUNTIME"]["IN_MEDAKA"]),
        features = ancient(partial(get_medaka_train_features, config=config))
    output:
        train_dir = directory("medaka_train{suffix,[^/]*}")
    log:
        "medaka_train{suffix}.log",
    params:
        opts = partial(get_opts, config=config, config_key="MEDAKA_TRAIN_OPTS"),
    threads: 8
    resources:
        gpu = 1
    shell:
        """
        check_files_exist {config[RUNTIME][CHECK_FILES_EXIST_OPTS]} {input} &> {log}
        echo "GPU status before" >> {log}
        gpustat >> {log}

        GPU=$(pick_gpu 2>> {log})

        echo "Runnning on host $HOSTNAME GPU $GPU" >> {log}

        if [ "{config[RUNTIME][SCRATCH]}" != "" ];then
            if [ "{config[RUNTIME][TMPSCRATCH]}" != "" ];then
                tmpscr={config[RUNTIME][TMPSCRATCH]}
                echo "Using provided TMPSCRATCH {config[RUNTIME][TMPSCRATCH]}" >> {log}
                mkdir -p $tmpscr &>> {log}
            else
                userscr={config[RUNTIME][SCRATCH]}/$USER
                mkdir -p $userscr 2>> {log}
                tmpscr=$(mktemp -d -p $userscr) &>> {log}
            fi
            t=$(date +"%T")
            echo "$t: Copying feature files to specified scratch directory: $tmpscr." >> {log}
            for f in {input.features}; do
                d=$tmpscr/$f
                t0=$(date +"%T")
                if [[ ! -e $d ]]; then
                    echo "$t0 Copying $d" >> {log}
                    mkdir -p $(dirname $d) && sleep 1 && cp $f $d &>> {log}
                    t1=$(date +"%T")
                    echo "$t1 Copied $d" >> {log}
                else
                    echo "Found existing file: $d" >> {log}
                fi
            done
            features=$(for f in {input.features}; do echo $tmpscr/$f; done)
        else
            features={input.features}
        fi

        set +u; {config[RUNTIME][SOURCE]} {input.venv} set -u;
        CUDA_VISIBLE_DEVICES=$GPU medaka train $features --train_name {output.train_dir} {params.opts} --threads_io {threads} &>> {log}
        """
